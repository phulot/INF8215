{
 "cells": [
  {
   "cell_type": "markdown",
   "metadata": {
    "collapsed": true
   },
   "source": [
    "# Correction TP2\n",
    "\n",
    "## 3.3 Les Termes\n",
    "Pour la première question concerne il s'agit d'evaluer les quantités données. On trouve alors que Jean possède la chat. Pour la seconde question on demande quel animal possède Pierre. deux réponses sont possibles :\n",
    "```\n",
    "?- possede(pierre,X).\n",
    "X=chien;\n",
    "X=cheval.\n",
    "?- possede(pierre,X),animal(X).\n",
    "X=chien.\n",
    "```"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## 3.7 L'arbre généalogique\n",
    "La première question ne présentait pas de difficulté particulière. Le seconde question comportait un petit piège : il fallait s'assurer que la règle ne renvoie pas soi même, notament que l'on ne peut être frère de soi même.\n",
    "```prolog\n",
    "frere(X,Y) :- pere(Z,X) , pere(Z,Y), homme(X), X\\=Y.\n",
    "```\n",
    "J'attire aussi votre attention sur la façon dont prolog cherche les résulats : selon l'implémentation il est possible que prolog renvoie deux fois le même résultat. Ceci conduit à des sous obtimalité dans les algorithmes, puisque plus de branches sont crées dans l'arbre. Ainsi on préfèrera \n",
    "```prolog\n",
    "frere(X,Y) :- pere(Z,X) , pere(Z,Y), homme(X), X\\=Y.\n",
    "```\n",
    "à \n",
    "```prolog\n",
    "frere(X,Y) :- parent(Z,X) , parent(Z,Y), homme(X), X\\=Y.\n",
    "```"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## 4.3 Premières règles\n",
    "1. La fonction sum a été bien implementée par tout le monde. \n",
    "\n",
    "2. Pour la fonction max deux implémentations sont possibles la première inspirée des autres langages est :\n",
    "```\n",
    "max2(X,Y,R) :- (X<Y,R is Y);(X>=Y, R is X).\n",
    "```\n",
    "La seconde est plus propre à prolog est vous mets sur la piste d'une meilleure utilisation de prolog\n",
    "```\n",
    "max2(X,X,X).\n",
    "max2(X,Y,X):-X>Y.\n",
    "max2(X,Y,Y):-X<Y.\n",
    "```\n",
    "\n",
    "pour le maximum de trois chiffres une ligne suffisait : \n",
    "```\n",
    "max2(X,Y,Z,R):-max2(X,Y,R1),max2(R1,Z,R).\n",
    "```\n",
    "une solution en plus de ligne etait inefficace. \n",
    "\n",
    "**Attention** ici, la solution :\n",
    "```\n",
    "max2(X,Y,R) :- X<Y,R is Y; R is X.\n",
    "```\n",
    "n'est pas correcte. Dans le cas où Y est supérieur à X cette fonction renvoie Y et X. Deux solutions pour la rendre correcte :\n",
    "```\n",
    "max2(X,Y,R) :- X<Y,R is Y,!; R is X.\n",
    "```\n",
    "ou\n",
    "```\n",
    "max2(X,Y,R) :- X<Y -> R is Y; R is X.\n",
    "```\n",
    "\n",
    "3. Pour le calcul de la dérivée il suffisait de mettre en place les règles de dérivation, prolog s'occupait ensuite tout seule de la calculer. \n",
    "```\n",
    "d(C,X,0) :- atomic(C), C\\=X.\n",
    "d(X,X,1).\n",
    "d(X^N, X, D):-N1 is N-1, D=N*X^N1.\n",
    "d(U+V, X, DU+DV) :- d(U, X, DU), d(V, X, DV).\n",
    "d(U-V, X, DU-DV) :- d(U, X, DU), d(V, X, DV).\n",
    "d(U*V, X, U*DV+V*DU) :- d(U, X, DU), d(V, X, DV).\n",
    "```\n",
    "Il ne fallait pas oublier la multiplication de deux polynomes. Seules ces règles étaient demandées. \n"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## 4.5 Arbre de recherche\n",
    "1. Pour cette question il ne fallait pas oublier la terminaisons de l'arbre, et donc d'y inscrire les False en fin de branche. \n",
    "\n",
    "L'arbre demandé était le suivant (arbre du groupe 5):\n",
    "\n",
    "![arbre proposé par le groupe 5](arbre.jpg)\n",
    "\n",
    "2. Le mot croisé n'a pas posé de problème, la solution la plus simple étant la suivante :\n",
    "\n",
    "```\n",
    "word(abalone, a, b, a, l, o, n, e).\n",
    "word(abandon, a, b, a, n, d, o, n).\n",
    "word(enhance, e, n, h, a, n, c, e).\n",
    "word(anagram, a, n, a, g, r, a, m).\n",
    "word(connect, c, o, n, n, e, c, t).\n",
    "word(elegant, e, l, e, g, a, n, t).\n",
    "\n",
    "crossword(V1,V2,V3,H1,H2,H3):-\n",
    "\tword(V1,_,X11,_,X21,_,X31,_),\n",
    "\tword(V2,_,X12,_,X22,_,X32,_),\n",
    "\tword(V3,_,X13,_,X23,_,X33,_),\n",
    "\tword(H1,_,X11,_,X12,_,X13,_),\n",
    "\tword(H2,_,X21,_,X22,_,X23,_),\n",
    "\tword(H3,_,X31,_,X32,_,X33,_).\n",
    "```\n",
    "Il y avait alors deux solution, symétriques par rapport à la diagonale. \n"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {
    "collapsed": true
   },
   "source": [
    "## 4.8 Listes et récursivité\n",
    "La plus grosse difficulté de cette partie a été les cas de base voici les solutions des problèmes:\n",
    "\n",
    "1. maximum d'une liste\n",
    "```\n",
    "max([X],X).\n",
    "max([X|T],M) :-  max(T,M1),  max2(X,M1,M).\n",
    "```\n",
    "2. somme d'une liste\n",
    "```\n",
    "somme([],0).\n",
    "somme([X|Y],S) :- somme(Y,S1) , S is X+S1.\n",
    "```\n",
    "3. n-ième élément\n",
    "```\n",
    "nth(1,[X|_],X).\n",
    "nth(N,[_|T],R) :- N>0, N1 is N-1 , nth(N1,T,R) .\n",
    "```\n",
    "4. zip\n",
    "```\n",
    "zip([],[],[]).\n",
    "zip([A|C],[B|D],[[A,B]|X]):-zip(C,D,X).\n",
    "```\n",
    "5. enumerate: attention ici on a demandé les entier de 0 à N-1 et pas de N-1 à 0. plusieurs solutions sont possibles, la plus élégante et rapide est : \n",
    "```\n",
    "enumerate(N,L):-enumerate(0,N,L).\n",
    "enumerate(N,N,[]):-!.\n",
    "enumerate(C,N,[C|L]):-C1 is C+1,enumerate(C1,N,L).\n",
    "```\n",
    "6. Rendre monnaie. Je mets ici la solution du groupe 16 qui a proposé une solution synthétique en utilisant efficacement l'évaluation arithmetique en prolog ainsi que la coupure (!).\n",
    "```\n",
    "rend_monnaie(Argent, Prix) :- Diff is Argent - Prix, Diff >= 0,\n",
    "                              enumerate(floor(Diff/2)+2, L),\n",
    "                              member(A,L), member(B,L), member(C,L), member(D,L), member(E,L),\n",
    "                              Diff =:= 2*A + 1*B + 0.25*C + 0.10*D + 0.05*E,\n",
    "                              format('A rendre: ~n~d pieces de 2$~n~d pieces de 1$~n~d pieces de 25c~n~d pieces de 10c~n~d pieces de 5c~n', [A,B,C,D,E]), !.\n",
    "```\n"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## 4.9 Logigrammes\n",
    "Ces problème ont plutot bien été réussi, des solutions plus ou moins compliquées on été proposés. Je présente ici une solution qui cherche à trouver la place de chaque élément du problème. L'interpretation étant ensuite laissée à l'utilisateur. \n",
    "\n",
    "**Problème 1**:\n",
    "```\n",
    "?- use_module(library(clpfd)).\n",
    "\n",
    "logigram(Var):-\n",
    "    Var = [A,E,J,Ja,Se,Es,MR,MB,MJ],\n",
    "    Var ins 1..3,\n",
    "    A#\\=E,A#\\=J,J#\\=E,\n",
    "    Ja#\\=Se,Se#\\=Es,Ja#\\=Es,\n",
    "    MB#\\=MR,MR#\\=MJ,MJ#\\=MB,\n",
    "    A#=MR,Ja#=E,J#=Es-1,Es#=MB-1,\n",
    "    label(Var).\n",
    "```\n",
    "on obtient les solutions suivantes : \n",
    "$X = [2, 3, 1, 3, 1, 2, 2, 3, 1].\n",
    "X = [3, 1, 2, 1, 2, 3, 3, 2, 1] $ \n",
    "\n",
    "Le serpent correspond à la 5 ième variable donc il est en position 1, dans la première solution, comme le Japonais. Le seconde solution trouve le même résultat. Le Japonais possède donc le serpent.\n",
    "La commande label sert à forcer prolog à evaluer les varaibles. \n",
    "Selon comment on mettait les commandes de position relative il y avait plus ou moins de solutions proposés. Cela est du à l'interchangeabilité des personnes. \n",
    "\n",
    "**Problème 2**:\n",
    "de la même façon on a le code suivant \n",
    "```\n",
    "% true si X et Y son à côtés\n",
    "acote(X,Y):-X#=Y+1.\n",
    "acote(X,Y):-X#=Y-1.\n",
    "\n",
    "% affichage de la solution\n",
    "print_([],[],_).\n",
    "print_([H|L],[N|V],N):-\n",
    "    write(H),write(', '),\n",
    "    print_(L,V,N).\n",
    "print_([_|L],[N1|V],N):-\n",
    "    N1#\\=N,\n",
    "    print_(L,V,N).\n",
    "\n",
    "print__(Var,X):-\n",
    "    L = [anglais,espagnol,ukrainien,norvegien,japonais,chien,escargot,renard,cheval,zebre,rouge,verte,blanche,jaune,bleue,cafe,the,lait,jus,eau,acrobate,diplomate,sculteur,medecin,violoniste],\n",
    "    print_(L,Var,X).\n",
    "\n",
    "logigram(Var):-\n",
    "    Var = [A,E,U,N,J,Chien,Escargot,Renard,Cheval,Zebre,Rouge,Verte,Blanche,Jaune,Bleue,Cafe,The,Lait,Jus,Eau,Acrobate,Diplomate,Sculteur,Medecin,Violoniste],\n",
    "    Var ins 1..5,\n",
    "    A#=Rouge,E#=Chien,Verte#=Cafe,U#=The,Verte#=Blanche+1,Sculteur#=Escargot,\n",
    "    Diplomate#=Jaune,Lait#=3,N#=1,acote(Medecin,Renard),acote(Diplomate,Cheval),\n",
    "    Violoniste#=Jus,J#=Acrobate,acote(N,Bleue),\n",
    "    all_distinct([A,E,U,N,J]),\n",
    "    all_distinct([Chien,Escargot,Renard,Cheval,Zebre]),\n",
    "    all_distinct([Rouge,Verte,Blanche,Jaune,Bleue]),\n",
    "    all_distinct([Cafe,The,Lait,Jus,Eau]),\n",
    "    all_distinct([Acrobate,Diplomate,Sculteur,Medecin,Violoniste]),\n",
    "    write('-----------------Eau---------------------'),nl,\n",
    "    print__(Var,Eau),nl,\n",
    "    write('-----------------Zebre-------------------'),nl,\n",
    "    print__(Var,Zebre).\n",
    "```\n",
    "on obtient la solution suivante :\n",
    "```\n",
    "-----------------Eau---------------------\n",
    "norvegien, renard, jaune, eau, diplomate, \n",
    "-----------------Zebre-------------------\n",
    "japonais, zebre, verte, cafe, acrobate,\n",
    "```\n",
    "C'est donc le norvegien qui boit l'eau et le japonais qui possède le zèbre."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {
    "collapsed": true
   },
   "outputs": [],
   "source": []
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Python 3",
   "language": "python",
   "name": "python3"
  },
  "language_info": {
   "codemirror_mode": {
    "name": "ipython",
    "version": 3
   },
   "file_extension": ".py",
   "mimetype": "text/x-python",
   "name": "python",
   "nbconvert_exporter": "python",
   "pygments_lexer": "ipython3",
   "version": "3.5.3"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 0
}
